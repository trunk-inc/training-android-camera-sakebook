apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"

    defaultConfig {
        applicationId "com.sakebook.android.sample.adobecamera"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        multiDexEnabled true

        buildConfigField("String", "CREATIVE_SDK_CLIENT_ID", "${CREATIVE_SDK_CLIENT_ID}")
        buildConfigField("String", "CREATIVE_SDK_CLIENT_SECRET", "${CREATIVE_SDK_CLIENT_SECRET}")
        manifestPlaceholders = [appPackageName: "${applicationId}"]

        if(rootProject.file('private.properties').exists()) {
            def props = new Properties()
            props.load(new FileInputStream(rootProject.file('private.properties')))
            buildConfigField("String", "CREATIVE_SDK_CLIENT_ID", props['CREATIVE_SDK_CLIENT_ID'])
            buildConfigField("String", "CREATIVE_SDK_CLIENT_SECRET", props['CREATIVE_SDK_CLIENT_SECRET'])
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/DEPENDENCIES'
        pickFirst 'AndroidManifest.xml'
    }

    dexOptions {
        jumboMode true
    }
    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:3.0.1'
        resolutionStrategy.eachDependency { details ->
            def requested = details.requested
            if (requested.group == 'com.android.support') {
                if (!requested.name.startsWith("multidex")) {
                    details.useVersion '25.3.1'
                }
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile ("com.android.support:appcompat-v7:26.1.0") {
        exclude group: 'com.android.support', module: 'cardview-v7'
    }
    compile ("com.android.support:multidex:1.0.1") {
        exclude group: 'com.android.support', module: 'cardview-v7'
    }

    compile ('com.adobe.creativesdk.foundation:auth:0.9.1251') {
        exclude group: 'com.android.support', module: 'support-v4'
    }
    compile ('com.adobe.creativesdk:image:4.8.4') {
        exclude group: 'com.android.support', module: 'support-v4'
    }
    compile 'com.localytics.android:library:4.0.1'
    compile ("org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version") {
        exclude group: 'org.jetbrains', module: 'annotations'
    }
}
repositories {
    mavenCentral()
    google()
}
